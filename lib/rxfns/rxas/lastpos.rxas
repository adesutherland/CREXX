/* lastpos
lastpos: procedure = .int
arg needle = .string, haystack = .string, start = 1
*/
          .globals=0
lastpos() .locals=13 .expose=rxfnsb.lastpos
   .meta "rxfnsb.lastpos"="b" ".int" lastpos() "needle = .string, haystack = .string, start = 1" ""
    brtpandt havestart,a3,1
    load a3,0
    isub a0,a0,1
    load r12,0
havestart:
    copy r1,a2          /* source string      */
    strlen r3,r1        /* length of source   */
    ieq r11,a3,0        /* 0 : no end pos parm is specified, check full length */
    brt nopos,r11       /* true:  full length */
    igt r11,a3,r3       /* specified new pos>string length? */
    brt nopos,r11       /* use original length */
    igt r11,a3,0        /* specified new pos>0? */
    brf nopos,r11       /* <=0 used original length */
    copy r3,a3          /* new max seek pos  */
nopos:
    load r11,"max search len "
    dec  r3
    copy r4,a1          /* search string      */
    strlen r10,r4
    dec r10             /* -1 for BCT         */
outerloop:
    strchar r8,r1,r3    /* load source char    */
    copy r5,r10         /* length search string*/
    copy r6,r10         /* inner offset        */
 /* test first search char  */
    strchar r9,r4,r6
    ieq r11,r8,r9       /* is it not equal, goto next source char */
    brf next,r11
    copy r12,r3         /* it is equal, save begin pos */
    ieq r11,r6,0        /* if remaining length is 0, return now*/
    brt found,r11
    dec r6              /* look into next search char  */
innerloop:
/* and test all other search char  */
    dec r3              /* look into next source char  */
    strchar r8,r1,r3    /* fetch source char           */
    strchar r9,r4,r6    /* fetch search char           */
    ieq r11,r8,r9       /* are they equal ?            */
    brf next,r11        /* branch into outer loop      */
    bctnm innerloop,r6  /* continue with next chars    */
    br  found
    copy r3,r12         /* re position to original source char */
next:
    bctnm outerloop,r3  /* outer loop */
    load r3,-1          /* string not found, will be later 0 */
found:
    inc r3              /* +1 to make offset to position */
    ret r3
    
