* cREXX Assembler Test Suite - UTF8

.globals=2
g0 .expose=tests.success
g1 .expose=tests.failure

main()         .locals=1
               say "cREXX Assembler Test Suite - UTF8 - Version cREXX-Phase-0 v0.1.6-f0022\n"

               * Zero result count
               load g0,0
               load g1,0

               * Run Tests
               call test_appendchar()
               call test_strlen()

               * Print Results
               iadd r0, g0, g1
               itos r0
               ssay r0
               say " tests run\n"
               brt failures, g1

               * No failures
               say "Success\n"
               ret 0

failures:      * There were failures
               itos g1
               say "*** "
               ssay g1
               say " tests failed ***\n"
               ret 1

expect_true()  .locals=0
               brt expect_true_ok,a1
               iadd g1,g1,1 * Failure
               say " FAILED\n"
               ret
expect_true_ok:
               iadd g0,g0,1 * Success
               say " ok\n"
               ret

expect_false() .locals=0
               brf expect_false_ok,a1
               iadd g1,g1,1 * Failure
               say " FAILED\n"
               ret
expect_false_ok:
               iadd g0,g0,1 * Success
               say " ok\n"
               ret

* Tests

test_appendchar()  .locals=5
               load r3,1 * One argument when calling expect_true/false
               say "Testing appendchar ..."
               load r1, "ΑΒΓΔ"
               load r2,923 * Greek Capital Letter Lambda Λ
               appendchar r1,r2
               seq r4,r1,"ΑΒΓΔΛ"
               call r0,expect_true(),r3
               ret

test_strlen()  .locals=6
               load r3,1 * One argument when calling expect_true/false
               say "Testing strlen 1 ..."
               load r1, "ΑΒΓΔ"
               strlen r2,r1
               ieq r4,r2,4
               call r0,expect_true(),r3
               say "Testing strlen 2 ..."
               copy r5,r1
               strlen r2,r5
               ieq r4,r2,4
               call r0,expect_true(),r3
               say "Testing strlen 3 ..."
               load r1, "ΑΒΓΔ"
               load r5, "QWERTYUIOPQWERTYUIOPQWERTYUIOP"
               concat r1,r1,r5
               strlen r2,r1
               ieq r4,r2,34
               call r0,expect_true(),r3
               ret
